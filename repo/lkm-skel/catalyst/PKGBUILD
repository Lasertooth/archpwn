# Maintainer: Vi0L0 <vi0l093@gmail.com>
# Great Contributor: Eduardo "kensai" Romero (previous maintainer)
# Great Contributor: Shen Miren <dickeny@gmail.com> (build_module/hook idea and code)
# Contributor: cyberpatrol
# Contributor: Armin Wehrfritz

_kernver=2.6.34-ARCH

pkgname=catalyst
pkgver=10.7
pkgrel=1
pkgdesc="AMD/ATI drivers for Radeon brand cards. Auto re-compile the fglrx module when kernel update."
arch=('i686' 'x86_64')
url="http://www.ati.amd.com"
license=('custom')
depends=('xorg-server<1.9.0' 'kernel26-pwn>=2.6.29' 'kernel26-pwn<2.6.36' 'kernel26-headers' 'netkit-bsd-finger' 'libstdc++5' 'libxrandr' 'libsm' 'fontconfig' 'libxcursor' 'libxi' 'gcc-libs' 'gcc>4.0.0' 'make')
optdepends=('lib32-catalyst-utils: 32-bit OpenGL applications support for x86_64 systems')
replaces=('catalyst-nostock' 'libgl')
conflicts=('catalyst-test' 'catalyst-utils' 'nvidia' 'libgl' 'xf86-video-ati' 'xf86-video-radeonhd' 'ati-dri')
provides=('catalyst-utils' 'libgl' "libatical=${pkgver}")
install=${pkgname}.install

#patch_file="fglrx-2.6.34.patch"
#patch_md5="5d464f7068fb45a9d2013cbd6a0b2021"
#patch_ver="2.6.34 2.6.35"

source=(http://www2.ati.com/drivers/linux/ati-driver-installer-${pkgver/./-}-x86.x86_64.run
        catalyst.sh
        amdcccle.desktop
        atieventsd.sh)
md5sums=('0855d05f303a442aa493c8542e2993ef'
         'bdafe749e046bfddee2d1c5e90eabd83'
         '4efa8414a8fe9eeb50da38b5522ef81d'
         'f729bf913613f49b0b9759c246058a87')

build() {
  ## Unpack archive
  msg "Unpacking archive ..."
  /bin/sh ./ati-driver-installer-${pkgver/./-}-x86.x86_64.run --extract archive_files
}

package() {
  ##Build kernel modules
  if [ "${CARCH}" = "x86_64" ]; then
    BUILDARCH=x86_64
    _archdir=x86_64
  fi
  if [ "${CARCH}" = "i686" ]; then
    BUILDARCH=i386
    _archdir=x86
  fi

  cd "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod"
  cp "${srcdir}/archive_files/arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a.GCC4" .
  cp 2.6.x/Makefile .

  make -C /lib/modules/${_kernver}/build SUBDIRS="`pwd`" ARCH=${BUILDARCH} modules

  install -m755 -d "${pkgdir}/lib/modules/${_kernver}/video/"
  install -m644 fglrx.ko "${pkgdir}/lib/modules/${_kernver}/video/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"

  sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/" $startdir/*.install

  ## Install userspace tools and libraries
  msg "Installing userspace tools and libraries ..."

  # Create directories
  install -m755 -d "${pkgdir}/etc/ati"
  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 -d "${pkgdir}/etc/profile.d"
  install -m755 -d "${pkgdir}/etc/acpi/events"

  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/dri"
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/drivers"
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/linux"

  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -d "${pkgdir}/usr/sbin"

  install -m755 -d "${pkgdir}/usr/include/X11/extensions"
  install -m755 -d "${pkgdir}/usr/include/GL"

  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m755 -d "${pkgdir}/usr/share/ati/amdcccle"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m755 -d "${pkgdir}/usr/share/man/man8"
  install -m755 -d "${pkgdir}/usr/share/pixmaps"
  install -m755 -d "${pkgdir}/opt/${pkgname}"

  # X.org driver
  if [ "${CARCH}" = "i686" ]; then
	cd "${srcdir}/archive_files/x750/usr/X11R6/lib/modules"
  elif [ "${CARCH}" = "x86_64" ]; then
	cd "${srcdir}/archive_files/x750_64a/usr/X11R6/lib64/modules"
  fi

  #install -m644 *.a "${pkgdir}/usr/lib/xorg/modules/"
  install -m755 *.so "${pkgdir}/usr/lib/xorg/modules/"
  install -m755 drivers/*.so "${pkgdir}/usr/lib/xorg/modules/drivers/"
  install -m755 linux/*.so "${pkgdir}/usr/lib/xorg/modules/linux/"
  install -m755 extensions/libglx.so "${pkgdir}/usr/lib/xorg/modules/extensions/"
  #install -m755 extensions/libdri.so "${pkgdir}/usr/lib/xorg/modules/extensions/libdri.ati"

  # Controlcenter / libraries
  if [ "${CARCH}" = "i686" ]; then
	cd "${srcdir}/archive_files/arch/x86/usr"
	_lib=lib
  elif [ "${CARCH}" = "x86_64" ]; then
	cd "${srcdir}/archive_files/arch/x86_64/usr"
	_lib=lib64
  fi

  install -m755 X11R6/bin/* "${pkgdir}/usr/bin/"
  install -m755 sbin/* "${pkgdir}/usr/sbin/"
  install -m755 X11R6/${_lib}/*.so* "${pkgdir}/usr/lib/"
  install -m644 X11R6/${_lib}/*.a "${pkgdir}/usr/lib/" # really needed?
  install -m644 X11R6/${_lib}/*.cap "${pkgdir}/usr/lib/"
  install -m755 X11R6/${_lib}/modules/dri/*.so "${pkgdir}/usr/lib/xorg/modules/dri/"
  install -m755 ${_lib}/*.so* "${pkgdir}/usr/lib/"

  ln -sf /usr/lib/xorg/modules/dri ${pkgdir}/usr/lib/dri
  ln -sf libfglrx_dm.so.1.0 "${pkgdir}/usr/lib/libfglrx_dm.so.1"
  ln -sf libfglrx_pp.so.1.0 "${pkgdir}/usr/lib/libfglrx_pp.so.1"
  #ln -sf libfglrx_tvout.so.1.0 "${pkgdir}/usr/lib/libfglrx_tvout.so.1"
  ln -sf libfglrx_gamma.so.1.0 "${pkgdir}/usr/lib/libfglrx_gamma.so.1"
  ln -sf libGL.so.1.2 "${pkgdir}/usr/lib/libGL.so.1"
  ln -sf libGL.so.1.2 "${pkgdir}/usr/lib/libGL.so"

  cd "${srcdir}"/archive_files/common
  install -m644 etc/ati/* "${pkgdir}/etc/ati/"
  chmod 755 "${pkgdir}/etc/ati/authatieventsd.sh"

  install -m644 usr/X11R6/include/X11/extensions/*.h "${pkgdir}/usr/include/X11/extensions/"
  install -m644 usr/X11R6/bin/amdupdaterandrconfig "${pkgdir}/usr/bin/"
  install -m644 usr/include/GL/*.h "${pkgdir}/usr/include/GL/"
  install -m755 usr/sbin/*.sh "${pkgdir}/usr/sbin/"
  install -m644 usr/share/ati/amdcccle/* "${pkgdir}/usr/share/ati/amdcccle/" # ? what are these files for?
  # install -m644 usr/share/gnome/apps/amdcccle.desktop "${pkgdir}/usr/share/applications/"
  install -m644 usr/share/icons/*.xpm "${pkgdir}/usr/share/pixmaps/"
  install -m644 usr/share/man/man8/*.8 "${pkgdir}/usr/share/man/man8/"
  install -m644 "${srcdir}/amdcccle.desktop" "${pkgdir}/usr/share/applications/"

  # ACPI example files
  install -m755 usr/share/doc/fglrx/examples/etc/acpi/*.sh "${pkgdir}/etc/acpi/"
  sed -i -e 's/usr\/X11R6/usr/g' "${pkgdir}/etc/acpi/ati-powermode.sh"
  install -m644 usr/share/doc/fglrx/examples/etc/acpi/events/* "${pkgdir}/etc/acpi/events/"

  # Add ATI Events Daemon launcher
  install -m755 "${srcdir}/atieventsd.sh" "${pkgdir}/etc/rc.d/atieventsd"

  # thanks to cerebral, we dont need that damned symlink
  install -m755 "${srcdir}/catalyst.sh" "${pkgdir}/etc/profile.d/"

  # License
  install -m644 "${srcdir}/archive_files/ATI_LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/"

  # hw_ati_hwdb
  install -m644 "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod/fglrxko_pci_ids.h" "${pkgdir}/opt/${pkgname}/"
}
